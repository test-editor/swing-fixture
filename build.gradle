plugins {
    id 'maven'
    id 'java'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'net.researchgate.release' version '2.6.0'
}

repositories {
    jcenter()
    maven { url "$projectDir/src/test/resources" }
}

group = 'org.testeditor.fixture'

dependencies {
    compile 'org.testeditor.fixture:core-fixture:3.1.0'
    compile 'org.easytesting:fest-swing:1.2.1'
    compile 'org.easytesting:fest-assert:1.4'
    compile 'org.easytesting:fest-reflect:1.2'
    testCompile 'de.akquinet.birthday:GeburtstagsListenVerwaltung:1.0'
    testCompile 'junit:junit:4.12'
}

sourceSets.main.resources.srcDirs = [ "src/main/java" ]
sourceSets.main.resources.includes = [ "**/*.aml" ]   

/* 
 * code below copied from core-fixture
 */
sourceCompatibility = 1.8
targetCompatibility = 1.8

// show standard out during test to see logging output
test.testLogging.showStandardStreams = true

jar {
    manifest {
        attributes 'Implementation-Title': "${project.group}.${project.name}",
                   'Implementation-Version': project.version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.packaging = 'jar'
    }
}

release {
    preTagCommitMessage = '[release]'
    tagCommitMessage = '[release]'
    newVersionCommitMessage = '[release] new version'
    tagTemplate = 'v${version}'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true 
    pkg {
        repo = 'Fixtures'
        name = project.name
        userOrg = 'test-editor'
        licenses = ['EPL-1.0']
        vcsUrl = "https://github.com/test-editor/${project.name}.git"
        version {
            name = project.version
            vcsTag = "v$project.version"
        }
        websiteUrl = 'http://testeditor.org'
    }
}
